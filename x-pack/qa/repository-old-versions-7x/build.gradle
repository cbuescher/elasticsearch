import org.elasticsearch.gradle.internal.test.RestIntegTestTask
import org.elasticsearch.gradle.testclusters.StandaloneRestIntegTestTask
import org.elasticsearch.gradle.Version

apply plugin: 'elasticsearch.internal-java-rest-test'
apply plugin: 'elasticsearch.rest-resources'

restResources {
  restApi {
    include '_common', 'search'
  }
  restTests {
    includeCore 'search/390_doc_values_search.yml'
  }
}

tasks.named("javaRestTest") {
  enabled = false
}

['7.16.3', '7.17.25'].each { versionString ->
  String versionNoDots = versionString.replace('.', '_')

  tasks.register("javaRestTest#${versionNoDots}", StandaloneRestIntegTestTask) {
    systemProperty 'tests.old_cluster_version', versionString
    usesDefaultDistribution()
    testClassesDirs = sourceSets.javaRestTest.output.classesDirs
    classpath = sourceSets.javaRestTest.runtimeClasspath
    usesBwcDistribution(Version.fromString(versionString))
  }

  tasks.named("check").configure {
    dependsOn "javaRestTest#${versionNoDots}"
  }
}
